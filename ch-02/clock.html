<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8" />
    <title>case study: clock</title>
    <style type="text/css">
      body {
        background: black;
        margin: 0px;
        padding: 0px;
        overflow: hidden;
      }
    </style>
  </head>

  <body onload="main()">
    <canvas id="example" width="400" height="400">
      Please use a browser that supports "canvas"
    </canvas>
    <script src="../lib/webgl-utils.js"></script>
    <script src="../lib/webgl-debug.js"></script>
    <script src="../lib/cuon-utils.js"></script>
    <script src="../lib/cuon-matrix.js"></script>
    <script src="clock.js"></script>

    <script id="vertexShader" type="x-shader/x-vertex">#version 300 es
      in vec4 a_Position;
      in vec2 a_Uv;
      out vec2 v_Uv;

      void main()	{
        v_Uv = a_Uv;
				gl_Position = a_Position;
			}
    </script>

    <script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
      precision highp float;

      uniform float u_HwRatio;
      uniform float u_Time;
      in vec2 v_Uv;
      out vec4 fragColor;

      //original by iq: https://www.shadertoy.com/view/lsXGz8
      //animation inspired by https://zhuanlan.zhihu.com/p/39798563?utm_source=ZHShareTargetIDMore&utm_medium=social&utm_oi=70060592857088

      float circle(vec2 pixel, vec2 center, float radius, float blurSize);
      float line(vec2 pixel, vec2 pa, vec2 pb, float width, float blurSize);
      mat2 rotateMatrix(float angle);

      void main() {
        vec2 uv = v_Uv - 0.5;
        uv.x /= u_HwRatio;
          
        //shake the clock
        float tt=mod(u_Time,1.5)/1.5;
        float ss=1.0+0.5*sin(tt*3.1415*6.0+uv.x*0.5)*exp(-tt*4.0);
        uv*=(vec2(0.7,1.5)+ss*vec2(0.3,-0.5));
      
        //paint the clock
        float circleBodySign = circle(uv, vec2(0.0, 0.0), 0.3, 0.005);
        vec4 clockBodyColor = mix(vec4(0.7, 0.7, 0.7, 1.0), vec4(0, 0.0, 0.0, 0.0), circleBodySign);
        
        float circleBodyWhiteSign = circle(uv, vec2(0.0, 0.0), 0.28, 0.005);
        vec4 clockBodyWhiteColor = mix(vec4(1.0, 1.0, 1.0, 1.0), vec4(0.0, 0.0, 0.0, 0.0), circleBodyWhiteSign);
        
        fragColor = vec4(clockBodyColor);
        fragColor += clockBodyWhiteColor;
        
        mat2 rotationSecond = rotateMatrix(u_Time + 45.0/3.14);   
        float secondLineSign = line(uv, vec2(0.0, 0.2) * rotationSecond, vec2(0.0, -0.03) * rotationSecond, 0.005, 0.005);
        fragColor = mix(vec4(1.0, 0.0, 0.0, 1.0), fragColor, secondLineSign);
        
        mat2 rotationMinute = rotateMatrix(u_Time/5.0 + 15.0/6.283);
        float minuteLineSign = line(uv, vec2(0.0, 0.15) * rotationMinute, vec2(0.0, -0.03) * rotationMinute, 0.007, 0.005);
        fragColor = mix(vec4(0.0, 0.0, 0.0, 1.0), fragColor, minuteLineSign);
        
        mat2 rotationHour = rotateMatrix(u_Time/10.0 + 45.0/6.283);
        float hourLineSign = line(uv, vec2(0.0, 0.1) * rotationHour, vec2(0.0, -0.03) * rotationHour, 0.009, 0.005);
        fragColor = mix(vec4(0.0, 0.0, 0.0, 1.0), fragColor, hourLineSign);
      }

      float circle(vec2 pixel, vec2 center, float radius, float blurSize) {
        vec2 vectorPixelToCenter = pixel - center;
        float distanceToCenter = length(vectorPixelToCenter);
        
        return smoothstep(radius - blurSize, radius, distanceToCenter);
      }

      float line(vec2 p, vec2 a, vec2 b, float width, float blur) {
        vec2 pa = p - a;
        vec2 ba = b - a;
        vec2 dir = normalize(ba);
        float t = clamp(dot(pa, dir), 0., length(ba));
        float dist = length(pa - dir * t);
        return smoothstep(width - blur, width, dist);
      }

      mat2 rotateMatrix(float angle) {
        return mat2(cos(angle), sin(angle), -sin(angle), cos(angle));
      }
    </script>
  </body>
</html>
